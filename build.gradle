buildscript {

    ext {
        kotlin_version = '1.3.61'
    }

    ext.PluginsVersions = [
            "GradlePluginV"      : "3.5.3",
            "KotlinPluginV"      : "1.3.50",
            "EasyLauncherPluginV": "1.3.1",
            "FirebasePluginV"    : "4.3.1",
            "KtlintPluginV"      : "2.1.1",
            "FabricPluginV"      : "1.28.1",
            "NavigationPluginV"  : "2.2.0-rc04",
            "VortexPluginV"      : "1.0.0",
            "RealmPluginV"       : "7.0.0-beta-SNAPSHOT"
    ]

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://dl.bintray.com/yt98/Vortex" }
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${rootProject.ext.PluginsVersions.GradlePluginV}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${rootProject.ext.PluginsVersions.KotlinPluginV}"
        classpath "com.akaita.android:easylauncher:${rootProject.ext.PluginsVersions.EasyLauncherPluginV}"
        classpath "com.google.gms:google-services:${rootProject.ext.PluginsVersions.FirebasePluginV}"
        classpath "org.jmailen.gradle:kotlinter-gradle:${rootProject.ext.PluginsVersions.KtlintPluginV}"
        classpath "io.fabric.tools:gradle:${rootProject.ext.PluginsVersions.FabricPluginV}"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${rootProject.ext.PluginsVersions.NavigationPluginV}"
        classpath "gradle.plugin.io.vortex.gradle.plugin.dependencies:vortex-dependencies-plugin:${rootProject.ext.PluginsVersions.VortexPluginV}"
        classpath "io.realm:realm-gradle-plugin:${rootProject.ext.PluginsVersions.RealmPluginV}"
    }

}

apply from: "Libraries.gradle"
apply from: "AppDetails.gradle"

allprojects {

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.bintray.com/yt98/Vortex" }
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
    }

}

allprojects {
    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            arg("dagger.formatGeneratedSource", "disabled")
            arg("dagger.gradle.incremental", "enabled")
        }
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500"
    }
}

void databaseConfiguration(configuration) {
    def RoomConfiguration = rootProject.ext.RoomConfiguration
    configuration.implementation RoomConfiguration.RoomRuntime
    configuration.kapt RoomConfiguration.RoomCompiler
    configuration.implementation RoomConfiguration.RoomKotlin
    configuration.implementation RoomConfiguration.RoomRxJava
}
